pipeline {
    agent any

    options {
        // 최신 빌드 5개만 보존
        buildDiscarder(logRotator(numToKeepStr: '5'))
        // 빌드 타임아웃 (30분 이상 걸리면 종료)
        timeout(time: 30, unit: 'MINUTES')
    }

    environment {
        IMAGE_NAME   = "kyumin19/be"
        IMAGE_TAG    = "${BUILD_NUMBER}"
        DOCKER_IMAGE = "${IMAGE_NAME}:${IMAGE_TAG}"
        DISCORD_WEBHOOK = credentials('discord-webhook')
    }

    stages {
        stage('Checkout') {
            steps {
                git branch: 'main',
                    url: 'https://github.com/TeamOldYoung/BE.git'
            }
        }

        stage('Build') {
            steps {
                dir('oldYoung') {
                    // 의존성만 변경된 경우 캐시 활용
                    sh '''
                        ./gradlew bootJar -x test \
                          --build-cache --console=plain \
                          -Dorg.gradle.jvmargs="-Xmx256m -Xms128m"
                    '''
                }
                // 빌드가 끝난 시점에 커밋 정보 확보
                script {
                    dir('oldYoung') {
                        env.COMMIT_INFO = sh(
                            script: "git log -1 --pretty=format:'%h | %an | %s'",
                            returnStdout: true
                        ).trim()
                    }
                }
            }
        }

        stage('Docker Build & Push') {
            steps {
                dir('oldYoung') {
                    script {
                        docker.withRegistry('https://index.docker.io/v1/', 'dockerhub-credentials') {
                            def image = docker.build("${DOCKER_IMAGE}")
                            image.push()
                            image.push("latest")
                            echo "✅ Docker 이미지 푸시 성공: ${DOCKER_IMAGE}"
                        }
                    }
                }
            }
        }
    }

    post {
        success {
            script {
                sendDiscordNotification("✅ Jenkins 빌드 성공!\\n이미지: ${DOCKER_IMAGE}")
            }
        }
        failure {
            script {
                sendDiscordNotification("🚨 Jenkins 빌드 실패!\\n빌드: #${BUILD_NUMBER}")
            }
        }
        always {
            cleanWs()
        }
    }
}

def sendDiscordNotification(String message) {
    def commitInfo = (env.COMMIT_INFO ?: "정보 없음").replace('"', '\\"')
    def fullMessage = "${message}\\n커밋: ${commitInfo}"
    
    withCredentials([string(credentialsId: 'discord-webhook', variable: 'WEBHOOK_URL')]) {
        sh """
            curl -s -X POST "\${WEBHOOK_URL}" \
            -H "Content-Type: application/json" \
            -d '{"content": "${fullMessage}"}' || echo "Discord 알림 실패"
        """
    }
}
